plugins {
  id "com.diffplug.gradle.spotless" version "3.16.0"
  id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

ext {
  jacocoVersion         = "0.8.2"
  junitVersion          = "5.3.1"
  logbackVersion        = "1.2.3"
  lombokVersion         = "1.18.4"
  mockitoVersion        = "2.23.0"
  slf4jVersion          = "1.7.25"
}

allprojects  {
  apply plugin: 'maven'
  apply plugin: 'io.spring.dependency-management'

  group = 'com.purplepip'
  version = '1.0.0-SNAPSHOT'

  dependencyManagement {
    dependencies {
      dependency "ch.qos.logback:logback-classic:${logbackVersion}"
      dependency "org.projectlombok:lombok:${lombokVersion}"
      dependency "org.slf4j:slf4j-api:${slf4jVersion}"
      dependency "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
      dependency "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
      dependency "org.mockito:mockito-core:${mockitoVersion}"
    }
  }

  repositories {
    mavenLocal()
    maven { url "https://packagecloud.io/purplepip/releases/maven2" }
    maven { url "http://repo.maven.apache.org/maven2" }
  }

  task hello {
    doLast { task ->
      println "Hello, ${task.project.name}!"
    }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: "jacoco"
  apply plugin: 'com.diffplug.gradle.spotless'

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  tasks.withType(JavaCompile) {
  	options.encoding = 'UTF-8'
  }

  jacoco {
    toolVersion = jacocoVersion
  }

  test {
    finalizedBy jacocoTestReport, hello
    useJUnitPlatform()
    testLogging {
      showStandardStreams = true
    }
  }
  
  spotless {
    java {
      googleJavaFormat()
    }
  }

  hello {
    doLast { task ->
      println "... subproject : ${task.project.name}"
    }
  }
}

task helloWorld {
  doLast { task ->
    println "Hello, World!"
  }
}
helloWorld.dependsOn hello
